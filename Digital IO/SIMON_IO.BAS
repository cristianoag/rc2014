01 REM SIMON MEMORY GAME IN SIMPLE BASIC (NEEDS DIGITAL IO BOARD)
02 REM BY CRISTIANOAG@HOTMAIL.COM
03 REM MAXROUNDS: DEFINES THE NUMBER OF ROUNDS FOR THE GAME, MAX IS 50 
04 REM LOAD THE GAME TO MEMORY AND RUN IT. THE DIGITAL IO WILL LIGHT A SEQUENCE OF LEDS STARTING WITH 1 AND
05 REM INCREASING BY ONE MORE EACH ROUND. WAIT FOR THE FLASH AND THEN REPEAT THE SEQUENCE PUSHING THE LEDS
06 REM IN THE SAME SEQUENCE. 

10 OUT 0,0
20 CLEAR
30 MAXROUNDS = 3
40 COUNT = 0
50 DIM R(50) 
60 DIM A(50)
70 GOSUB 1000
100 REM GAME STARTS
110 COUNT = COUNT + 1
120 FOR X = 1 TO MAXROUNDS
130 REM PRINT THE SEQUENCE
140 FOR Y = 1 TO COUNT
143 OUT 0,0
145 GOSUB 1500
150 OUT 0,2^R(Y):GOSUB 1400:OUT 0,0:GOSUB 1400
160 NEXT Y
180 REM THE HUMAN GOES AFTER THE FLASH
185 OUT 0,255:GOSUB 2100:OUT 0,0
190 FOR W = 1 TO COUNT
195 PRINT "READY FOR HUMAN INPUT"
200 GOSUB 1700 
202 REM PRINT "A(W)=";A(W)
204 REM PRINT "R(W)A=";2^R(W)
210 IF A(W) <> 2^R(W) THEN PRINT "YOU LOST!": OUT 0, 255: End
215 GOSUB 1500
220 NEXT W
240 COUNT = COUNT + 1
400 NEXT X
410 PRINT "YOU WIN!"
420 GOSUB 2000
430 END

1000 REM RANDOMIZE ARRAY
1010 FOR XX = 1 TO 50
1020 R(XX) = INT(RND(7)*7)
1030 NEXT XX
1040 RETURN

1200 REM PRINT ARRAY
1210 FOR YY = 1 TO 50
1220 PRINT R(YY)
1230 NEXT YY
1240 RETURN

1400 REM DEFAULT WAIT LOOP
1410 FOR Z = 1 TO 2000: NEXT Z: RETURN

1500 REM SMALL WAIT LOOP
1510 FOR Z = 1 TO 700: NEXT Z: RETURN

1700 REM KEY READ
1705 OUT 0,0
1710 FOR P = 1 TO 50000
1705 GOSUB 1500
1720 A(W) = INP(0)
1725 OUT 0,INP(0)
1730 IF A(W) <> 0 THEN RETURN
1740 NEXT P

2000 REM YOU WIN SEQUENCE
2005 FOR H = 1 TO 3
2010 FOR F=0 TO 7
2020 OUT 0,2^F
2030 GOSUB 2100
2040 NEXT F
2050 FOR F=7 TO 0 STEP -1
2060 OUT 0,2^F
2070 GOSUB 2100
2080 NEXT F
2085 OUT 0,0
2088 NEXT H
2090 RETURN
2100 FOR I = 1 TO 150 : NEXT I : RETURN
